// For bnd
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.3.0'
	}
}

plugins {
	id 'java'
	id 'maven'
	id 'signing'
}

apply plugin: 'biz.aQute.bnd.builder'

group = 'org.daisy.dotify'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {	java { srcDir 'src' }	resources { srcDir 'src' } }
	test {	java { srcDir 'test' }	resources { srcDir 'test' } }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/8/docs/api/')
}

repositories {
	mavenCentral()
	//mavenLocal()
	maven { url "https://oss.sonatype.org/content/groups/staging" }
	//maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
	compileOnly 'org.daisy.streamline:streamline-api:1.2.0'
	compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
	compile 'com.vladsch.flexmark:flexmark:0.28.14'
	compile 'com.vladsch.flexmark:flexmark-ext-autolink:0.28.14'
	compile 'com.vladsch.flexmark:flexmark-ext-tables:0.28.14'
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.28.14'
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-tasklist:0.28.14'
	
	testCompile 'org.daisy.streamline:streamline-engine:1.2.0'
	testCompile group: "junit", name: "junit", version: "4.12"
}

// Note that this jar currently is NOT compatible with OSGi. It's just built with the OSGi bundle tool
// like all other projects for simplicity. If OSGi compatibility is added at a later time, the configuration
// below should be a good start.
// See also: https://github.com/brailleapps/dotify.markdown/issues/1
jar {
	manifest {
		attributes(
			'-sources': 'true',
			'Import-Package': 'org.daisy.streamline.api.tasks; provide:=true, *',
			//'Private-Package': 'org.daisy.dotify.markdown.impl',
			'Service-Component': '*',
			'Automatic-Module-Name': "$moduleName",
			'Bundle-Name': "$moduleName",
			'Bundle-SymbolicName': "$moduleName",
			'Built-By': System.getProperty("user.name"),
			'Built-On': new Date().format('yyyy-MM-dd'),
			'Repository-Revision': "$repoRevision",
			'Repository-URL': "$repositoryURL",
			'Include-Resource': 'LICENSE, NOTICE'
		)
	}
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
	doLast {
		copy {
			from "src"
			include "**/doc-files/*.*"
			into new File(docsDir, "javadoc")
		}
	}
}

task zip(type: Zip, description: 'Packages the bundle for plugin distribution') {
	from jar
	from configurations.runtime
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
    repositories {
        mavenDeployer {
            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name 'dotify.markdown'
                packaging 'jar'
                description 'Dotify Markdown'
                url "$repositoryURL"
                scm {
                    connection "$repositorySCM"
                    developerConnection "$repositorySCM"
                    url "$repositoryURL"
                }
                licenses {
                    license {
                        name 'LGPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                    }
                }
                developers {
                    developer {
                        id 'joel'
                        name 'Joel HÃ¥kansson'
                    }
                }
            }
        }
    }
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}